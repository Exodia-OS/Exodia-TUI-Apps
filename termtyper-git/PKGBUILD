#
# Maintainer: Mahmoud Mohamed (Ozil) <mmsaeed509@gmail.com> , <https://github.com/mmsaeed509>
#
# Developer : Murli Tawari <https://github.com/kraanzu>
#

pkgname=termtyper-git
_gitname=termtyper
pkgver=2.0.0.r49.g6be4ace
pkgrel=1
pkgdesc="A TUI application written in python for typing!"
arch=('any')
url="https://github.com/kraanzu/termtyper"
license=('MIT')

depends=(
    
    'python-rich'
    'python-textual'
    'python-playsound'
   
    )

makedepends=(
   
    'python-build'
    'python-installer'
    'python-wheel'
    'python-poetry'
    'git'
   
    )

groups=('Exodia-TUI-Apps')

provides=(termtyper)
conflicts=(termtyper)
source=("git+$url")
md5sums=('SKIP')



pkgver() {
	cd "${_gitname}"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "${_gitname}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${_gitname}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/termtyper/LICENSE"
    install -Dm644 README.md "${pkgdir}/usr/share/doc/termtyper/README.md"
}
